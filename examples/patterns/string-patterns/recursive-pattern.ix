manifest {}

# string patterns can be recursive, using '@' in front of a pattern will cause the interpreter
# to defer the evaluation of the right side until all sub patterns are defined.
# recursive patterns are pretty limited and slow: don't use them to check/parse complex strings, use real parsers instead.
pattern json-list = @ %str( 
    '[' 
        (| atomic-json-val
         | json-val 
         | ((json-val ",")* json-val) 
        )? 
    ']'
)

pattern json-val = @ %str( (| json-list | atomic-json-val ) )
pattern atomic-json-val = "1"

print "[] match json-val:" ("[]" match json-val)
print "[1] match json-val:" ("[1]" match json-val)
print "[[]] match json-val:" ("[[]]" match json-val)
print "[[1]] match json-val:" ("[[1]]" match json-val)
print "[[1,[]]] match json-val:" ("[[1,[]]]" match json-val)

print "`[` match json-val:" ("[" match json-val)
print "`[1` match json-val:" ("[1" match json-val)
print "`[1,` match json-val:" ("[1," match json-val)