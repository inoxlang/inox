const (
    WS_HOST = wss://localhost:8080
    WS_URL = @WS_HOST/
)

manifest {
    permissions: {
        read: {
            WS_URL
            IWD_PREFIX # allow reading the working dir
        }
        write: WS_URL
        provide: {
            WS_HOST
            https://localhost:8080
        }
    }
    limits: {
        "ws/simul-connections": 1
    }
}

# server

globalvar ws_server = ws.Server!()

fn handle(rw http.resp-writer, r http.req){
    conn = ws_server.upgrade!(rw, r)

    # echo
    msg = conn.read_json!()
    conn.send_json(msg)
}

server = http.Server!(https://localhost:8080, handle)

# client
conn = ws.connect!(WS_URL, --insecure)
conn.send_json!({data: "hello"})
assign msg err = conn.read_json()

if err? {
    print(err)
} else {
    print "message from server:" 
    print(msg)
}

# this line will fail because the number of simultaneous connections is 1
# other_conn = ws.connect!($WS_URL)

conn.close()
server.close()